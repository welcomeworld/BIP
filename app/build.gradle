apply plugin: 'com.android.application'
apply plugin: 'bugly'

bugly {
    appId = '2d6af2e6f5' // 注册时分配的App ID
    appKey = '97b88401-8537-4364-8767-e679b1e0765c' // 注册时分配的App Key
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) + 20
    }
    catch (ignored) {
        println "===================error code!"
        return 1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        println "===================error name!"
        return "1.0.00"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.github.welcomeworld.bangumi.instrumentality.project"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8

        targetCompatibility JavaVersion.VERSION_1_8

    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation 'org.jsoup:jsoup:1.13.1'

    implementation 'org.jdeferred:jdeferred-android-aar:1.2.4'

    implementation 'com.github.DylanCaiCoding.ViewBindingKTX:viewbinding-base:2.0.2'
    implementation "androidx.room:room-runtime:2.2.6"
    annotationProcessor 'androidx.room:room-compiler:2.2.6'
//    implementation "androidx.room:room-rxjava3:2.2.6"
//    implementation 'org.litepal.guolindev:core:3.2.3'

    //友盟
    implementation "com.umeng.umsdk:common:9.4.4"
    implementation "com.umeng.umsdk:asms:1.4.1"

    implementation "com.umeng.umsdk:apm:1.5.2"
//    implementation  'com.umeng.umsdk:abtest:1.0.0'

    implementation project(path:':devbase')

    implementation 'com.alibaba:fastjson:1.1.72.android'

    implementation 'com.eclipsesource.j2v8:j2v8:5.0.103@aar'

    implementation 'com.github.welcomeworld:DanmakuFlameMaster-Jitpack:v0.9.25'

    //bugly
    implementation 'com.tencent.bugly:crashreport:3.4.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'

    implementation 'com.github.welcomeworld:BIPPlayer:1.0.05'
    implementation 'com.nisigada:devbase:1.0.2'
    implementation project(path:':app_update')


}