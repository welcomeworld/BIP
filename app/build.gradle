apply plugin: 'com.android.application'

Properties keyStoreProperties = new Properties()
keyStoreProperties.load(rootProject.file("keystore/keystore.properties").newDataInputStream())

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) + 20
    }
    catch (ignored) {
        println "===================error code!"
        return 1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        println "===================error name!"
        return "1.0.00"
    }
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.github.welcomeworld.bangumi.instrumentality.project"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(keyStoreProperties['storeFile'])
            storePassword keyStoreProperties['storePassword']
            keyAlias keyStoreProperties['keyAlias']
            keyPassword keyStoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8

        targetCompatibility JavaVersion.VERSION_1_8

    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.1'
    implementation 'androidx.navigation:navigation-ui:2.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation 'org.jsoup:jsoup:1.13.1'

    implementation 'org.jdeferred:jdeferred-android-aar:1.2.4'

    implementation 'com.github.DylanCaiCoding.ViewBindingKTX:viewbinding-base:2.0.2'
    implementation "androidx.room:room-runtime:2.4.3"
    annotationProcessor 'androidx.room:room-compiler:2.4.3'
//    implementation "androidx.room:room-rxjava3:2.2.6"
//    implementation 'org.litepal.guolindev:core:3.2.3'

    //友盟
    implementation "com.umeng.umsdk:common:9.4.4"
    implementation "com.umeng.umsdk:asms:1.4.1"

    implementation "com.umeng.umsdk:apm:1.5.2"
//    implementation  'com.umeng.umsdk:abtest:1.0.0'

    implementation 'com.alibaba:fastjson:1.1.72.android'

    implementation 'com.eclipsesource.j2v8:j2v8:5.0.103@aar'

    implementation 'com.github.welcomeworld:DanmakuFlameMaster-Jitpack:v0.9.25'

    //bugly
    implementation 'com.tencent.bugly:crashreport:3.4.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'

    //custom library
    implementation 'com.github.welcomeworld:BIPPlayer:1.0.20'
    implementation 'com.github.welcomeworld:DevTool:1.0.03'
    implementation project(path: ':app_update')
    implementation project(path: ':zxing')

    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
    //exoplayer
    def exoVersion = "2.18.1"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoVersion"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exoVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoVersion"
    implementation "com.google.android.exoplayer:exoplayer-rtsp:$exoVersion"

    def lifecycle_version = "2.5.1"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

}