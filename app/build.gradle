apply plugin: 'com.android.application'

Properties keyStoreProperties = new Properties()
keyStoreProperties.load(rootProject.file("keystore/keystore.properties").newDataInputStream())

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) + 20
    }
    catch (ignored) {
        println "===================error code!"
        return 1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        println "===================error name!"
        return "1.0.00"
    }
}

android {
    compileSdkVersion compile_version
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.github.welcomeworld.bangumi.instrumentality.project"
        minSdkVersion 21
        targetSdkVersion compile_version
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64", "x86"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(keyStoreProperties['storeFile'])
            storePassword keyStoreProperties['storePassword']
            keyAlias keyStoreProperties['keyAlias']
            keyPassword keyStoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8

        targetCompatibility JavaVersion.VERSION_1_8

    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'

    //glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation "com.google.code.gson:gson:$gson_version"

    implementation 'org.jsoup:jsoup:1.16.1'

    implementation 'org.jdeferred:jdeferred-android-aar:1.2.6'

    implementation 'com.github.DylanCaiCoding.ViewBindingKTX:viewbinding-base:2.0.2'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation 'com.github.welcomeworld:DanmakuFlameMaster-Jitpack:v0.9.25'

    //custom library
    implementation "com.github.welcomeworld:BIPPlayer:$bipplayer_version"
    implementation "com.github.welcomeworld:DevTool:$devtool_version"
    implementation project(path: ':app_update')
    implementation project(path: ':zxing')

    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    //exoplayer
    implementation "com.google.android.exoplayer:exoplayer-core:$exo_version"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exo_version"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exo_version"
    implementation "com.google.android.exoplayer:exoplayer-rtsp:$exo_version"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

}